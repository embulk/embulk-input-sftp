plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
    id "java"
    id "checkstyle"
    id "jacoco"
    id "org.embulk.embulk-plugins" version "0.4.0"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "org.embulk.input.sftp"
version = "0.3.4"
description = "Reads files stored on remote server using SFTP."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"
    compile("org.apache.commons:commons-vfs2:2.6.0") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
    }
    compile "commons-io:commons-io:2.6"
    compile "com.jcraft:jsch:0.1.55"

    testCompile "junit:junit:4.13"
    testCompile "org.embulk:embulk-core:0.9.23:tests"
    testCompile "org.embulk:embulk-standards:0.9.23"
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
    testCompile "org.apache.sshd:apache-sshd:1.1.0"
    testCompile "org.littleshoot:littleproxy:1.1.0-beta1"
    testCompile "io.netty:netty-all:4.0.34.Final"
}

embulkPlugin {
    mainClass = "org.embulk.input.sftp.SftpFileInputPlugin"
    category = "input"
    type = "sftp"
}

javadoc {
    options {
        locale = 'en_US'
        encoding = 'UTF-8'
    }
}

// add tests/javadoc/source jar tasks as artifacts to be released
task testsJar(type: Jar, dependsOn: classes) {
    classifier = 'tests'
    from sourceSets.test.output
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationTo???Repository".
            from components.java
            artifact testsJar
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// bintray
bintray {
    // write at your bintray user name and api key to ~/.gradle/gradle.properties file:
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

    publications = [ "embulkPluginMaven" ]
    publish = true

    pkg {
        userOrg = 'embulk-input-sftp'
        repo = 'maven'
        name = project.name
        desc = 'SFTP file input plugin for Embulk'
        websiteUrl = 'https://github.com/embulk/embulk-input-sftp'
        issueTrackerUrl = 'https://github.com/embulk/embulk-input-sftp/issues'
        vcsUrl = 'https://github.com/embulk/embulk-input-sftp.git'
        licenses = ['Apache-2.0']
        labels = ['embulk', 'java']
        publicDownloadNumbers = true

        version {
            name = project.version
        }
    }
}

gem {
    authors = [ "Satoshi Akama" ]
    email = [ "satoshiakama@gmail.com" ]
    summary = "SFTP file input plugin for Embulk"
    homepage = "https://github.com/embulk/embulk-input-sftp"
    licenses = [ "Apache-2.0" ]
}

gemPush {
    host = "https://rubygems.org"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
